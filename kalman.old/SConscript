Import('env', 'arch')

templates = Glob('*')
sympy_helpers = "sympy_helpers.py"
ekf_sym = "ekf_sym.py"

to_build = {
    'car': 'car_kf.py',
    'live': 'live_kf.py',
}


if arch != "aarch64":
    to_build.update({
        'lane': '#xx/pipeline/lib/ekf/lane_kf.py',
        'pos_computer_4': 'lst_sq_computer.py',
        'pos_computer_5': 'lst_sq_computer.py',
        'feature_handler_5': 'feature_handler.py',
        'loc_4': 'loc_kf.py',
        'gnss': 'gnss_kf.py',
    })

found = {}

for target, command in to_build.items():
    if File(command).exists():
        found[target] = command

for target, command in found.items():
    target_files = File([f'generated/{target}.cpp', f'generated/{target}.h'])
    command_file = File(command)
    env.Command(target_files,
                [templates, command_file, sympy_helpers, ekf_sym],
                command_file.get_abspath()+" "+target
    )

    env.SharedLibrary('generated/' + target, target_files[0])
